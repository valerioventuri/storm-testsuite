*** Settings ***
Resource   lib/stormlib.txt
Resource   lib/clientSRM.txt
Resource   lib/globus_util.txt
Resource   lib/lcg_util.txt
Resource   lib/dcache.txt

*** Test Cases ***

Prepare to get on an existent file using clientSRM
  [Tags]  storm-client  ptg
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${test_file}  Get a unique name
  Put without really putting using clientSRM  ${vo}  ${test_file}
  ${surl}  Build surl  ${vo}  ${test_file}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}
  Should Contain  ${output}  SRM_REQUEST_QUEUED
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -t ${token} -p
  Should Contain  ${output}  SRM_FILE_PINNED
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Prepare to get on an unexistent file using clientSRM
  [Tags]  storm-client  ptg
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${test_file}  Get a unique name
  ${surl}  Build surl  ${vo}  ${test_file}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_INVALID_PATH
  [Teardown]  Clear all credentials

Release file on an existent file using clientSRM
  [Tags]  storm-client  rf
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${test_file}  Get a unique name
  ${surl}  Build surl  ${vo}  ${test_file}
  Put without really putting using clientSRM  ${vo}  ${test_file}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform rf using clientSRM  ${surl}  ${token}
  Should Contain  ${output}  SRM_SUCCESS
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Put done an existent file using clientSRM
  [Tags]  storm-client  pd
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${test_file}  Get a unique name
  ${surl}  Build surl  ${vo}  ${test_file}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_SPACE_AVAILABLE
  ${output}  Perform pd using clientSRM  ${surl}  ${token}  -p
  Should Contain  ${output}  SRM_SUCCESS
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Test that the SRM service is able to transfer a file on the SRM endpoint
  [Tags]  storm-client  globus-utils
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${test_file}  Create local file
  ${surl}  Build surl  ${vo}  ${test_file}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p
  Should Contain  ${output}  SRM_SPACE_AVAILABLE
  Copy-out file using globus-utils  ${test_file}  ${vo}  ${test_file}
  ${output}  Perform pd using clientSRM  ${surl}  ${token}  -p
  Should Contain  ${output}  SRM_SUCCESS
  ${output}  Perform ls using clientSRM  ${surl}
  Should Contain  ${output}  SRM_SUCCESS
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Check failure when performing a ptp on a file already put
  [Tags]  storm-client  ptp
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${test_file}  Create local file
  ${surl}  Build surl  ${vo}  ${test_file}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p
  Copy-out file using globus-utils  ${test_file}  ${vo}  ${test_file}
  ${output}  Perform pd using clientSRM  ${surl}  ${token}  -p
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p -t ${token}
  Should Contain  ${output}  SRM_FAILURE
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Test that the SRM service is able to transfer a file from the SRM endpoint
  [Tags]  storm-client  ptg
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${test_file}  Create local file
  ${surl}  Build surl  ${vo}  ${test_file}
  Copy-out file using lcg-utils  ${test_file}  ${surl}
  ${output}  ${token}  Perform ptg using clientSRM  ${surl}  -p -T -P gsiftp
  ${result}  ${turl}=  Should Match Regexp  ${output}  transferURL=(\".+\")
  Copy-in file using gsiftp protocol  ${turl}  ${test_file}
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Check lcg-cp computed checksum in case it starts with zero
  [Documentation]  Regression test for https://storm.cnaf.infn.it:8443/redmine/issues/108. Given a file with ADLER32 checksum that starts with '0' and a SURL pointing to a non existent file in an existent folder, verify that after transfering the file on the SURL the checksum value computed for the file matches as string with the one of the local file.
  [Tags]  lcg-utils  regression  cp
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${srcFile}  Create local file with checksum that starts with zero
  ${srcSurl}  Build surl  ${vo}  ${srcFile}
  ${destFile}  Get a unique name
  ${destSurl}  Build surl  ${vo}  ${destFile}
  Copy-out file using lcg-utils  ${srcFile}  ${srcSurl}
  ${output}  Detailed ls using clientSRM  ${srcSurl}
  ${result}  ${b_checksum}=  Should Match Regexp  ${output}  checkSumValue=(\".+\")
  Log  ${b_checksum}
  Copy file using lcg-utils  ${srcSurl}  ${destSurl}
  ${output}  Detailed ls using clientSRM  ${destSurl}
  ${result}  ${a_checksum}=  Should Match Regexp  ${output}  checkSumValue=(\".+\")
  Log  ${a_checksum}
  Should be equal  ${b_checksum}  ${a_checksum}
  Perform rm using clientSRM  ${srcSurl}
  Perform rm using clientSRM  ${destSurl}
  [Teardown]  Clear all credentials

Test lcg-cp out
  [Tags]  lcg-utils  cp
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${surl}  Build surl  ${vo}  ${filename}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  Perform rm using clientSRM  ${surl}
  [Teardown]  Clear all credentials

Check if a ptp on an unexistant file with a wrong protocol return the right error message
  [Documentation]  Regression test for https://storm.cnaf.infn.it:8443/redmine/issues/127
  [Tags]  storm-client  regression
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Get a unique name
  ${surl}  Build surl  ${vo}  ${filename}
  ${output}  ${token}  Perform ptp using clientSRM  ${surl}  -p -T -P unknown_protocol
  Should Contain  ${output}  SRM_NOT_SUPPORTED
  [Teardown]  Clear all credentials

Check a file is correctly transferred out, re-transferred in and deleted with dcache client
  [Tags]  lcg-utils  dcache-client
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${surl}  Build surl  ${vo}  ${filename}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  Copy-in file using lcg-utils  ${surl}  ${filename}
  Remove file using dCache client  ${vo}  ${filename}
  [Teardown]  Clear all credentials

Check file copy in/out using lcg-utils, use dcache-client to create/remove dir and file
  [Tags]  lcg-utils  dcache-client
  [Setup]  Init certificate and voms proxy  test0  ${vo}
  ${filename}  Create local file
  ${output}  List files in directory using lcg_utils  ${vo}  ${filename}
  Should Contain  ${output}  No such file or directory
  ${dirName}  Get a unique name
  Create directory using dCache client  ${vo}  ${dirName}
  ${output}  Try to create directory using dCache client  ${vo}  ${dirName}
  Should Contain  ${output}  SRM_DUPLICATION_ERROR
  ${output}  List files in directory using lcg_utils  ${vo}  ${dirName}
  Should Not Contain  ${output}  SRM_INVALID_PATH
  ${surl}  Build surl  ${vo}  ${dirName}/${filename}
  Copy-out file using lcg-utils  ${filename}  ${surl}
  ${output}  List files in directory using lcg_utils  ${vo}  ${dirName}/${filename}
  Should Not Contain  ${output}  SRM_INVALID_PATH
  Copy-in file using lcg-utils  ${surl}  ${filename}
  Remove file using dCache client  ${vo}  ${dirName}/${filename}
  ${output}  Try to remove file using dCache client  ${vo}  ${dirName}/${filename}
  Should Contain  ${output}  SRM_FAILURE
  Remove directory using dCache client  ${vo}  ${dirName}
  ${output}  Try to remove directory using dCache client  ${vo}  ${dirName}  
  Should Contain  ${output}  SRM_INVALID_PATH
  [Teardown]  Clear all credentials